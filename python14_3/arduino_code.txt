//JSON데이터를 역직렬화하기위해서 필요함!
#include <ArduinoJson.h>
#include <Arduino.h>
#include <TM1637Display.h>

#define CLK1 2
#define DIO1 3
#define CLK2 4
#define DIO2 5

TM1637Display display1(CLK1, DIO1);
TM1637Display display2(CLK2, DIO2);

void setup() {
  //파이썬과 통신속도가 9600이다!(보-레이트가 9600이다)
  Serial.begin(9600);
  display1.setBrightness(0x0f);
  display2.setBrightness(0x0f);
  //FND모듈에 초기값으로 0을 출력한다!
  display1.showNumberDec(0, false);
  display2.showNumberDec(0, false);
}

void loop() {
  //파이썬에서 JSON데이터가 수신되면 종료문자인 \n이 있을때까지 읽는다!
  if(Serial.available() > 0){
    //아두이노의 수신버퍼에 데이터가 존재할경우 \n까지읽어서 myjson에 반환한다!
    String myjson = Serial.readStringUntil('\n');
    //myjson : 파이썬이 보낸 JSON데이터임!(역직렬화해볼까?)

    //일단 비어있는 object를 적정한 사이트에 맞게 만들어줌!
    StaticJsonDocument<96> doc;


    //파이썬에서 수신한 json을 역직렬화해서 doc에 대입해줌!
    DeserializationError error = deserializeJson(doc, myjson);
    //파이썬쪽에서 json규칙에 위배되는 데이터를 전송하는 경우 에러임!
    if (error) {
      Serial.print(F("deserializeJson() failed: "));
      Serial.println(error.f_str());
      return;
    }
    //{'redhorse': 0, 'greenhourse': 0}
    int red_horse = doc["redhorse"];
    int green_horse = doc["greenhourse"];

    display1.showNumberDec(red_horse, false);
    display2.showNumberDec(green_horse, false);
    
    Serial.print("빨간말이 검출된 갯수는 =");
    Serial.print(red_horse);
    Serial.print("개 이고, 녹색말이 검출된 갯수는 =");
    Serial.print(green_horse);
    Serial.println("개 입니다!");
  }
}


