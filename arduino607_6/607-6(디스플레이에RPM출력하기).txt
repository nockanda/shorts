#include <Encoder.h>
#include <Arduino.h>
#include <TM1637Display.h>

//모터1번
#define CLK1 12
#define DIO1 13
//모터2번
#define CLK2 A4
#define DIO2 A5
TM1637Display display1(CLK1, DIO1);
TM1637Display display2(CLK2, DIO2);

Encoder wheelLeft(2, 4); //모터1
Encoder wheelRight(3, 5); //모터2

long positionLeft  = 0;
long positionRight = 0;

#define ENA 6 //아날로그출력
#define IN1 7 //디지털출력
#define IN2 8 //디지털출력
#define IN3 9 //디지털출력
#define IN4 10 //디지털출력
#define ENB 11 //아날로그출력
#define btn1 A0 //후진
#define btn2 A1 //브레이크
#define btn3 A2 //전진

//이 예제에서는 속도는 항상 최대로 한다!

//IN3이 LOW고 IN4가 HIGH일때 정회전(시계방향)
//IN3이 HIGH고 IN4가 LOW일때 역회전(반시계방향)
//IN3과 IN4가 모두 LOW일때 break

unsigned long t = 0;

void setup() {
  Serial.begin(9600);
  pinMode(IN1,OUTPUT); //모터1
  pinMode(IN2,OUTPUT); //모터1
  pinMode(IN3,OUTPUT); //모터2
  pinMode(IN4,OUTPUT); //모터2

  pinMode(btn1,INPUT_PULLUP);
  pinMode(btn2,INPUT_PULLUP);
  pinMode(btn3,INPUT_PULLUP);

  display1.setBrightness(0x0f);
  display2.setBrightness(0x0f);
}

void loop() {
  
  if(digitalRead(btn1) == LOW){
    //후진
    nockanda_back();
  }
  if(digitalRead(btn2) == LOW){
    //정지
    nockanda_break();
  }
  if(digitalRead(btn3) == LOW){
    //전진
    nockanda_go();
  }
  

  //0.1초 간격을 생성한다!
  if(millis() - t > 100){
    t = millis();
    //가변저항값을 읽어와서 속도를 설정한다
    int analog = analogRead(A3); //0~1023
    analog = map(analog,0,1023,0,255);
    analogWrite(ENA,analog); //0~255
    analogWrite(ENB,analog); //0~255
    
    //현재 엔코더량을 가져온다
    long newLeft = wheelLeft.read();
    long newRight = wheelRight.read();

    float leftRPM = (newLeft*-1) * 600 / 900.0;
    float rightRPM = newRight * 600 / 900.0;

    display1.showNumberDec(leftRPM, false);
    display2.showNumberDec(rightRPM, false);

    Serial.print(leftRPM);
    Serial.print(",");
    Serial.print(rightRPM);
    Serial.println();

    //계산할거 다하고 현재 엔코더량을 초기화한다
    wheelLeft.write(0);
    wheelRight.write(0);
  }
}

void nockanda_go(){
  //모터1번은 반시계방향회전 모터2번은 시계방향회전
  digitalWrite(IN1,LOW);
  digitalWrite(IN2,HIGH);
  digitalWrite(IN3,LOW);
  digitalWrite(IN4,HIGH);
}
void nockanda_back(){
  //모터1번은 시계방향회전 모터2번은 반시계방향회전
  digitalWrite(IN1,HIGH);
  digitalWrite(IN2,LOW);
  digitalWrite(IN3,HIGH);
  digitalWrite(IN4,LOW);
}
void nockanda_break(){
  digitalWrite(IN1,LOW);
  digitalWrite(IN2,LOW);
  digitalWrite(IN3,LOW);
  digitalWrite(IN4,LOW);
}





