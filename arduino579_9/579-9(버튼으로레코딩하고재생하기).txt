//아두이노의 서보모터 내장 라이브러리
#include <Servo.h>

//서보모터 1개당 인스턴스 1개 필요
Servo myservo;

//아두이노의 디지털2번핀에 서보모터 제어핀이 연결되어있고
//아날로그 피드백선은 아날로그0번핀에 연결되어있다.
#define servo_pin 2
#define feedback A0
#define btn_rec 3 //레코딩
#define btn_run 4 //재생
#define led_rec 5 //레코딩 램프

int degree_min = 54; //서보의 각도가 0일때 아날로그값
int degree_max = 587; //서보의 각도가 180일때 아날로그값

//10초동안의 서보모터의 움직임을 기억한다!
byte memory_degree[1000];

void setup() {
  Serial.begin(9600); //9600의 통신속도로 PC와 통신하겠다!
  pinMode(btn_rec,INPUT_PULLUP);
  pinMode(btn_run,INPUT_PULLUP);
  pinMode(led_rec,OUTPUT);
  //서보모터가 연결된핀을 아두이노가 제어하겠다!
  myservo.attach(servo_pin);

  //서보모터의 0도일때의 가변저항값과 180도일때의 가변저항값을 스스로 학습한다
  //각도를0도로 회전하고 1초쉰다음 가변저항값 측정하기!
  myservo.write(0); 
  delay(1000);
  degree_min = analogRead(feedback);
  //각도를180도로 회전하고 1초쉰다음 가변저항값 측정하기!
  myservo.write(180); 
  delay(1000);
  degree_max = analogRead(feedback);
  //기본위치는 90도
  myservo.write(90); 
  delay(1000);
  //아두이노가 제어신호를 보내지 않음!(토크가 발생하지 않음)
  myservo.detach(); 
}

void loop() {
  if(digitalRead(btn_rec) == LOW){
    //버튼이 눌려졌다
    Serial.println("서보모터의 움직임을 기억하겠습니다!");
    digitalWrite(led_rec,HIGH);
    for(int i = 0;i<1000;i++){
      int analog = analogRead(feedback);
      int cal_degree = map(analog,degree_min,degree_max,0,180);
      if(cal_degree <0 ) cal_degree = 0; //하한값지정
      if(cal_degree >180) cal_degree = 180; //상한값지정
      memory_degree[i] = cal_degree;
      delay(10);
    }
    Serial.println("종료하겠습니다!");
    digitalWrite(led_rec,LOW);
  }
  if(digitalRead(btn_run) == LOW){
    //버튼이 눌려졌다
    Serial.println("서보모터의 움직임을 재생하겠습니다!");
    myservo.attach(servo_pin);
    for(int i = 0;i<1000;i++){
      myservo.write(memory_degree[i]); 
      delay(10);
    }
    Serial.println("종료하겠습니다!");
    myservo.detach(); 
  }

  //작업2번 시리얼입력을 받아서 각도를 설정하고 제어를 시작 중지하는 부분
  if(Serial.available()){
    //PC에서 문자 1개를 입력받겠다!
    char data = Serial.read();

    if(data == '1'){
      //10초간 레코딩시작
      
    }else if(data == '2'){
      //10초간 재생
      
    }
    
    
    
  }
}










