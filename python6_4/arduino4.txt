//JSON데이터를 역직렬화하기위해서 필요함!
#include <ArduinoJson.h>
#include <Servo.h>

#define btn1 3
#define btn2 4

StaticJsonDocument<96> doc_in;
StaticJsonDocument<96> doc_out;
Servo myservo;
unsigned long t = 0;
int mydegree = 0;

void setup() {
  //파이썬과 통신속도가 9600이다!(보-레이트가 9600이다)
  Serial.begin(9600);
  pinMode(btn1,INPUT_PULLUP);
  pinMode(btn2,INPUT_PULLUP);
  //서보모터가 9번에 연결됨!
  myservo.attach(9);
  //서보모터의 초기각도는 무조건 0도
  myservo.write(mydegree);
}

void loop() {
  
  //100밀리초간격으로 파이썬에게 led상태를 전송한다
  if(millis() - t > 100){
    t = millis();
    
    doc_out["degree"] = mydegree;
    String output = "";
    serializeJson(doc_out, output);
    //파이썬으로 전송!
    Serial.println(output);
  }
  
  

  if(digitalRead(btn1) == LOW){
    //10도씩 하강
    mydegree -= 10;
    if(mydegree < 0) mydegree = 0;
    myservo.write(mydegree);
    delay(100);
  }
  if(digitalRead(btn2) == LOW){
    //10도씩 상승
    mydegree += 10;
    if(mydegree > 180) mydegree = 180;
    myservo.write(mydegree);
    delay(100);
  }
  
  //파이썬에서 JSON데이터가 수신되면 종료문자인 \n이 있을때까지 읽는다!
  if(Serial.available() > 0){
    //아두이노의 수신버퍼에 데이터가 존재할경우 \n까지읽어서 myjson에 반환한다!
    String myjson = Serial.readStringUntil('\n');
    //myjson : 파이썬이 보낸 JSON데이터임!(역직렬화해볼까?)

    //일단 비어있는 object를 적정한 사이트에 맞게 만들어줌!
    

    //파이썬에서 수신한 json을 역직렬화해서 doc에 대입해줌!
    DeserializationError error = deserializeJson(doc_in, myjson);

    //파이썬쪽에서 json규칙에 위배되는 데이터를 전송하는 경우 에러임!
    if (error) {
      //Serial.print(F("deserializeJson() failed: "));
      //Serial.println(error.f_str());
      return;
    }
    int mydata = doc_in["servo"];

    if(mydata == 0){
      //10도 내리고
      //mydegree = mydegree - 10;
      mydegree -= 10;
      if(mydegree < 0) mydegree = 0;
    }else if(mydata == 1){
      //10도 올린다
      //mydegree = mydegree + 10;
      mydegree += 10;
      if(mydegree > 180) mydegree = 180;
    }
    //서보모터가 작동되는 지점
    myservo.write(mydegree);
  }
}


